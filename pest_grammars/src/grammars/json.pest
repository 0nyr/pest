// pest. The Elegant Parser
// Copyright (C) 2017  Drago»ô Tiselice
//
// This Source Code Form is subject to the terms of the Mozilla Public
// License, v. 2.0. If a copy of the MPL was not distributed with this
// file, You can obtain one at http://mozilla.org/MPL/2.0/.

json = { soi ~ (object | array) ~ eoi }

object = { "{" ~ pair ~ ("," ~ pair)* ~ "}" | "{" ~ "}" }
pair   = { string ~ ":" ~ value }

array = { "[" ~ value ~ ("," ~ value)* ~ "]" | "[" ~ "]" }

value = { string | number | object | array | bool | null }

string  = @{ "\"" ~ (escape | !("\"" | "\\") ~ any)* ~ "\"" }
escape  = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | unicode) }
unicode = @{ "u" ~ hex ~ hex ~ hex ~ hex }
hex     = { '0'..'9' | 'a'..'f' | 'A'..'F' }

number = @{ "-"? ~ int ~ ("." ~ '0'..'9'+ ~ exp? | exp)? }
int    = @{ "0" | '1'..'9' ~ '0'..'9'* }
exp    = @{ ("E" | "e") ~ ("+" | "-")? ~ '0'..'9'+ }

bool = { "true" | "false" }

null = { "null" }

whitespace = _{ " " | "\t" | "\r" | "\n" }
